apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = project.ext.groupId
version = project.ext.versionName

install {
    repositories.mavenInstaller {
        pom.project {
            name project.name
            description project.ext.description
            url project.ext.siteUrl
            inceptionYear '2017' // release date

            packaging 'aar'
            groupId project.ext.groupId
            artifactId project.ext.artifactId
            version project.ext.versionName

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm { // Optional setting
                connection project.ext.gitUrl
                developerConnection project.ext.gitUrl
                url project.ext.siteUrl
            }

            developers { // Optional setting
                developer {
                    id 'shhong'
                    name 'shhong'
                    email 'shnoble79@gmail.com'
                }
            }
        }

        pom.whenConfigured { pom ->
            pom.dependencies.forEach { dep ->
                if (dep.getVersion() == "unspecified") {
                    dep.setGroupId(project.ext.groupId)
                    dep.setVersion(project.ext.versionName)
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = project.ext.bintrayRepo
        name = project.name
        userOrg = project.ext.bintrayUserOrg
        desc = project.ext.description

        websiteUrl = project.ext.siteUrl
        issueTrackerUrl = project.ext.issuesUrl
        vcsUrl = project.ext.gitUrl

        licenses = ['Apache-2.0']
        labels = project.ext.bintrayLabels

        publish = true
        publicDownloadNumbers = true

        version {
            name = project.ext.versionName
            desc = "${project.ext.description} ${project.ext.versionName}"
            released = new Date()
            vcsTag = project.ext.versionName
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}